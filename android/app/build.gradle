apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
	compileSdkVersion 25
	buildToolsVersion '25.0.2'

	defaultConfig {
		applicationId 'technology.unrelenting.freepass'
		minSdkVersion 23
		targetSdkVersion 25
		multiDexEnabled true
		versionCode 1
		versionName '1.0'
		ndk {
			abiFilters 'x86'//, 'arm64-v8a', 'armeabi-v7a'//, 'armeabi'
		}
		externalNativeBuild {
			cmake {
				arguments '-DANDROID_PLATFORM=android-24', '-DANDROID_STL=stlport_shared', '-DANDROID_CPP_FEATURES=exceptions'
			}
        }
	}

	externalNativeBuild {
		cmake {
			path 'CMakeLists.txt'
		}
	}

	dexOptions {
		preDexLibraries true
		dexInProcess = true
	}

	dataBinding {
		enabled = true
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		debug {
			jniDebuggable true
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		main.jni.srcDirs = []
	}

	splits {
		abi {
			enable true
			reset()
			include 'x86'//, 'arm64-v8a', 'armeabi-v7a'//, 'armeabi'
			universalApk true
		}
	}

	packagingOptions {
		exclude 'META-INF/LICENSE'
	}
}

android.applicationVariants.all { variant ->
	// Don't move the task to the top level, it needs the `variant`!
    def javacppTask = task("build${variant.name.capitalize()}Javacpp") {
        inputs.file('src/main/kotlin/technology/unrelenting/freepass/Vault.kt')
        outputs.file('src/main/jni/jniVault.cpp')
        doLast {
            def path = configurations.compile.find { File file -> file.name.startsWith('javacpp') }.absolutePath
            javaexec {
                main = "org.bytedeco.javacpp.tools.Builder"
                classpath path
                args '-cp', variant.javaCompiler.destinationDir,
                        '-cp', variant.javaCompiler.destinationDir.toString()
                               .replace('intermediates/classes', 'tmp/kotlin-classes')
                               .replace('intermediates\\classes', 'tmp\\kotlin-classes'),
                        '-d', 'src/main/jni', '-nocompile', 'technology.unrelenting.freepass.Vault'
            }
        }
    }
    javacppTask.dependsOn variant.javaCompiler
    variant.externalNativeBuildTasks[0].dependsOn javacppTask
}

dependencies {
	compile fileTree(include: ['*.jar'], dir: 'libs')
	compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.2-4"
	compile "com.upokecenter:cbor:2.5"
	compile "io.reactivex.rxjava2:rxjava:2.1.0"
	compile "io.reactivex.rxjava2:rxkotlin:2.0.3"
	//compile "io.reactivex.rxjava2:rxandroid:2.0.1"
	//compile "com.jakewharton.rxbinding2:rxbinding-kotlin:2.0.0"
	compile "com.github.k-kagurazaka.rx-property-android:rx-property:3.1.0"
	compile "com.github.k-kagurazaka.rx-property-android:rx-property-kotlin:3.1.0"
	compile "org.bytedeco:javacpp:1.1"
	kapt "com.android.databinding:compiler:2.3.1"
}
